prototype(Kleisli.Neos.DocumentCollection:Document.DocumentCollection) < prototype(Neos.Fusion:Component) {

    defaultView = 'list'

    layoutView = Neos.Neos:Page {
        head.stylesheets.documentCollection = Kleisli.Neos.DocumentCollection:Document.Head.BackendStylesheets
        body = 'Define a layoutView in your Document prototype.'
        body.@process.documentCollection = Kleisli.Neos.DocumentCollection:Process.PrependAdminBar {
            header.currentView = 'layout'
        }
    }

    renderer = Neos.Fusion:Case {
        layoutView {
            @position = "start"
            condition =  ${!site.context.inBackend || request.arguments.view == 'layout' || (props.defaultView == 'layout' && request.arguments.view != 'list')}
            renderer = ${props.layoutView}
        }

        default {
            @position = "end"
            condition = true
            renderer = Neos.Neos:Page {
                head.stylesheets.documentCollection = Kleisli.Neos.DocumentCollection:Document.Head.BackendStylesheets
                bodyTag.attributes.class = "neos neos-module"

                body = Neos.Fusion:Component {
                    documents = Neos.Fusion:Loop {
                        items = ${q(node).children('[instanceof Neos.Neos:Document]').sort('date', 'ASC')}
                        itemName = 'document'
                        itemRenderer = Neos.Fusion:Component {
                            icon = ${document.nodeType.fullConfiguration.ui.icon}
                            renderer = afx`
                                <tr class={(document.hiddenInIndex ? 'document-hiddenInIndex ':'')+(document.hidden ? 'document-hidden ':'')}>
                                    <td>
                                        <Neos.Neos:NodeLink node={document}>
                                            <i class={'fas fa-' + props.icon + ' ' + props.icon} @if={props.icon}></i> {document.properties.title}
                                        </Neos.Neos:NodeLink>
                                    </td>

                                </tr>
                            `
                        }
                    }
                    documents.@process.table = ${'<table class="neos-table">'+value+'</table>'}

                    icon = ${documentNode.nodeType.fullConfiguration.ui.icon}

                    renderer = afx`
                        <Kleisli.Neos.DocumentCollection:Component.AdminBar currentView='list' />
                        <div class="neos-module-wrap">
                            <div class="neos-content neos-fluid-container neos-indented">
                                <legend>
                                    <i class={'fas fa-' + props.icon + ' ' + props.icon} @if={props.icon}></i> &nbsp;
                                    {documentNode.properties.title}
                                </legend>
                                {props.documents}
                            </div></div>
                    `
                }
            }
        }
    }


    @cache {
        mode = 'uncached'
        context {
            1 = 'node'
            2 = 'documentNode'
        }
    }

}
